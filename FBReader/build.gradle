apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    project.ext.set("versionMajor", "5")
    project.ext.set("versionMinor", "2")
    project.ext.set("versionRevision", "13")
    project.ext.set("versionCode","52")
    
    //NOTE: Changing the app name here will cause Jenkins to fail copying to anitfactory.
    //See the build Jenkins -> Configure -> Published Artifacts field
    project.ext.set("appName", "GoRead")

    def version = "${versionMajor}.${versionMinor}.${versionRevision}"

    defaultConfig {
        applicationId "org.benetech.android"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode = project.ext.get("versionCode").toInteger()
        versionName = project.ext.get("appName") + " " + "${version}"
        multiDexEnabled true

        ndk {
            moduleName "DeflatingDecompressor-v2"
        }

        signingConfigs {
            release {
                Properties signingProperties = new Properties ()
                signingProperties.load ( new FileInputStream (rootProject.file ( "sign.properties") ) )

                android.signingConfigs.release.storeFile file( signingProperties.keyStore )
                android.signingConfigs.release.keyAlias signingProperties.keyAlias
                android.signingConfigs.release.storePassword signingProperties.keyStorePassword
                android.signingConfigs.release.keyPassword signingProperties.keyAliasPassword
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles 'proguard.cfg'
                signingConfig signingConfigs.release
            }
        }

    }

    def apkFileName = project.ext.get("appName") + "-" + "${version}"
    if (System.getenv("BUILD_NUMBER"))
        apkFileName =  project.ext.get("appName") + "-" + "${version}_" + System.getenv("BUILD_NUMBER")

    project.archivesBaseName = apkFileName

    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
        arm {
            ndk {
                abiFilters "armeabi-v7a", "armeabi"
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.google.guava:guava:13.0.1'
    compile 'com.android.support:appcompat-v7:22.+'
    compile 'com.google.android.gms:play-services-analytics:8.1.0'
    compile 'com.google.android.gms:play-services:8.1.0'
    compile 'com.android.support:multidex:1.0.1'

    compile files('libs/bugsense3.4.jar')
    compile files('libs/commons-codec-1.4.jar')
    compile files('libs/commons-io-2.4.jar')
    compile files('libs/jmdns.jar')
    compile files('libs/zip4j_1.1.7.jar')
}
